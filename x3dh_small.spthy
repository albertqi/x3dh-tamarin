theory SmallX3DH
begin

builtins: diffie-hellman, hashing, signing, symmetric-encryption

rule IdentityKey:
    let ik = 'ik'^~sk in
    [ Fr(~sk) ]
    -->
    [ !PrivateIdentityKey($P, ~sk), !PublicIdentityKey($P, ik),
      Out(ik) ]

rule SignedPrekey:
    let spk = 'spk'^~sk in
    [ Fr(~sk) ]
    -->
    [ !PrivateSignedPrekey($P, ~sk), !PublicSignedPrekey($P, spk),
      Out(spk) ]

rule EphemeralKey:
    let ek = 'ek'^~sk in
    [ Fr(~sk) ]
    -->
    [ !PrivateEphemeralKey($P, ~sk), !PublicEphemeralKey($P, ek),
      Out(ek) ]

rule B_Init:
    let prekey_sig = sign(SPKB, ~pIKB) in
    [ !PrivateIdentityKey($B, ~pIKB), !PublicIdentityKey($B, IKB),
      !PublicSignedPrekey($B, SPKB) ]
    --[ ReachBInit() ]->
    [ St_B($B, $A),
      Out(<$B, $A, IKB, SPKB, prekey_sig>) ]

rule A_InitMsg:
    let DH1 = SPKB^~pIKA
        DH2 = IKB^~pEKA
        DH3 = SPKB^~pEKA
        SK = h(<DH1, DH2, DH3>)
        AD = <IKA, IKB> in
    [ !PrivateIdentityKey($A, ~pIKA), !PublicIdentityKey($A, IKA),
      !PrivateEphemeralKey($A, ~pEKA), !PublicEphemeralKey($A, EKA), Fr(~msg),
      In(<$B, $A, IKB, SPKB, prekey_sig>) ]
    --[ ReachAInitMsg(), ResponderKey(SK), MessageWasSent(~msg), SecretKey(SK),
        Eq(verify(prekey_sig, SPKB, IKB), true) ]->
    [ Out(<$A, $B, IKA, EKA, senc(~msg, SK), AD>) ]

rule B_RecMsg:
    let DH1 = IKA^~pSPKB
        DH2 = EKA^~pIKB
        DH3 = EKA^~pSPKB
        SK = h(<DH1, DH2, DH3>)
        AD = <IKA, IKB> in
    [ St_B($B, $A), !PrivateIdentityKey($B, ~pIKB), !PublicIdentityKey($B, IKB),
      !PrivateSignedPrekey($B, ~pSPKB),
      In(<$A, $B, IKA, EKA, msg_enc, AD_in>) ]
    --[ ReachBRecMsg(), InitiatorKey(SK),
        Eq(AD, AD_in) ]->
    []

rule LeakPrivateKeys:
    [ !PrivateIdentityKey($A, ~pIKA), !PrivateIdentityKey($B, ~pIKB),
      !PrivateSignedPrekey($B, ~pSPKB) ]
    --[ Leak() ]->
    [ Out(<~pIKA, ~pIKB, ~pSPKB>) ]

restriction Eq:
    "All t1 t2 #x. Eq(t1, t2) @ #x ==> t1 = t2"

lemma Executability:
    exists-trace
    "Ex #t1 #t2 #t3. ReachBInit() @ #t1 & ReachAInitMsg() @ #t2 & ReachBRecMsg() @ #t3"

lemma ResponderKeySecrecy:
    "All k #t. ResponderKey(k) @ #t ==> not Ex #x. K(k) @ #x"

lemma InitiatorKeySecrecy:
    "All k #t. InitiatorKey(k) @ #t ==> not Ex #x. K(k) @ #x"

lemma SendMsgSecrecy:
    "All m #t. MessageWasSent(m) @ #t ==> not Ex #x. K(m) @ #x"

lemma ForwardSecrecy:
    "All m #i #j. #i < #j & MessageWasSent(m) @ #i & Leak() @ #j ==> not Ex #x. K(m) @ #x"

end
